apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

buildscript {
    ext.kotlin_version = '1.6.10'

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
    }
}

repositories {
    google()
    mavenCentral()
}
def localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())
def keystoreProperties = new Properties()
keystoreProperties.load(rootProject.file("keystore.properties").newDataInputStream())

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId localProperties["id"]
        versionCode 67
        versionName "3.1"
        multiDexEnabled true
        minSdkVersion 23
        targetSdkVersion 31
        buildConfigField "String", "S_C_KEY", localProperties["s_c_key"]
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res', 'apollo/res']
            aidl.srcDirs = ['src', 'apollo/src']
            assets.srcDirs = ['assets']
            java {
                srcDir '../common/src/main/java'
                srcDir 'apollo/src'
                srcDir 'src'
            }
        }
    }


    signingConfigs {
        zig {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.zig
            applicationVariants.all { variant -> variant.outputs.all { outputFileName = "MediaPlay.apk" } }
        }
    }
    buildToolsVersion '32.1.0-rc1'
    buildFeatures { compose true }

    kotlinOptions {
        useIR = true
        jvmTarget = '11'
    }
    lint { abortOnError false }
    composeOptions { kotlinCompilerExtensionVersion "1.2.0-alpha05" }
    setProperty("archivesBaseName", 'MediaPlay')
}

dependencies {
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation platform('com.google.firebase:firebase-bom:26.3.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:20.1.2'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.2.9'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    //compose
    implementation 'androidx.compose.foundation:foundation:1.1.1'
    implementation 'androidx.compose.ui:ui:1.1.1'
    implementation 'androidx.compose.material:material:1.1.1'
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0'
    implementation "androidx.media:media:1.5.0"
    //ads
    implementation 'com.facebook.android:audience-network-sdk:6.8.0'
    //db
    def room_version = '2.4.2'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation("androidx.room:room-ktx:$room_version")
}